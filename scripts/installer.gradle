import org.gradle.internal.os.OperatingSystem


evaluationDependsOn(':apps:MavenMetaDataFixer')

interface ExecOperationsProvider {
   @javax.inject.Inject
   ExecOperations getExecOperations()
}
def execOperations = objects.newInstance(ExecOperationsProvider).execOperations

def metaDataScriptBatch = file("$rootDir/files/MavenMetaDataFixer.bat")
def metaDataScriptSh = file("$rootDir/files/MavenMetaDataFixer.sh")
def toolsUpdaterOutputDir = file("$buildDir/toolsUpdater")

def toolsUpdaterTask = tasks.register('toolsUpdater', Exec) {
  outputs.dir toolsUpdaterOutputDir
  workingDir = "$projectDir/apps/ToolsUpdater"

  commandLine 'dotnet', 'publish', '-c', 'Release', '-r', dotnetRuntime, "/p:Version=$pubVersion", "-o", toolsUpdaterOutputDir

  doLast {
    // We can sign the app because we have a Developer ID.
    if (project.hasProperty("developerID")) {
        println "Received a Developer ID -- signing the tools binary..."
        String identity = project.property("developerID")
        execOperations.exec {
            commandLine "sh", "-c", "automation/macos_codesign.sh ${identity} $toolsUpdaterOutputDir/ToolsUpdater"
        }
    }
  }
}

ext.msBuildExtraSetup = { AbstractArchiveTask zip->
  //zip.dependsOn msbuild
  zip.inputs.file project(':apps:MavenMetaDataFixer').shadowJar.archiveFile
  zip.inputs.file metaDataScriptBatch
  zip.inputs.file metaDataScriptSh
  zip.inputs.dir toolsUpdaterOutputDir
  zip.dependsOn toolsUpdaterTask

  zip.dependsOn project(':apps:MavenMetaDataFixer').shadowJar

  zip.from (project(':apps:MavenMetaDataFixer').shadowJar.archiveFile) {
    into '/maven'
    rename { 'MavenMetaDataFixer.jar' }
  }

  zip.from (toolsUpdaterOutputDir) {
    into '/tools'
    exclude '**/*.pdb'
    filePermissions { unix(0755) }
  }

  if (project.ext.isUnix == true) {
    zip.from(metaDataScriptSh) {
      into '/maven'
      filePermissions { unix(0755) }
    }
  } else {
    zip.from(metaDataScriptBatch) {
      into '/maven'
    }
  }
}
