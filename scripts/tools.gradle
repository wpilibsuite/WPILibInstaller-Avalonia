def toolsJson = file("$buildDir/tools.json")
def scriptBaseFile = file("files/ScriptBase.vbs")
def scriptBaseUnixFile = file("files/ScriptBase.sh")

def scriptBaseCppFile = file("files/ScriptBaseCpp.vbs")
def scriptBaseCppUnixFile = file("files/ScriptBaseCpp.sh")

def advantageScopeScriptFile = file("files/AdvantageScope.vbs")
def advantageScopeScriptUnixFile = file("files/AdvantageScope.sh")

def choreoScriptFile = file("files/Choreo.vbs")
def choreoScriptUnixFile = file("files/Choreo.sh")

def elasticScriptFile = file("files/Elastic.vbs")
def elasticScriptUnixFile = file("files/Elastic.sh")

def toolsJsonTask = tasks.register('toolsJson', Task) {

  dependsOn tasks.named('lazyModelEvaluation')

  doLast {
    def resolvedDeps = configurations.tools.resolvedConfiguration.resolvedArtifacts.toList()
    def config = []
    resolvedDeps.each {
      def item = [:]
      item['name'] = it.name
      item['version'] = it.moduleVersion.id.version
      def artif = [:]
      artif['classifier'] = it.classifier
      artif['extension'] = it.extension
      def id = it.moduleVersion.id
      artif['groupId'] = id.group
      artif['version'] = id.version
      artif['artifactId'] = id.name
      item['artifact'] = artif
      item['cpp'] = false
      config << item
    }

    def resolvedCppDeps = configurations.cppTools.resolvedConfiguration.resolvedArtifacts.toList()
    resolvedCppDeps.each {
      def item = [:]
      item['name'] = it.name
      item['version'] = it.moduleVersion.id.version
      def artif = [:]
      artif['classifier'] = it.classifier
      artif['extension'] = it.extension
      def id = it.moduleVersion.id
      artif['groupId'] = id.group
      artif['version'] = id.version
      artif['artifactId'] = id.name
      item['artifact'] = artif
      item['cpp'] = true
      config << item
    }

    def advantageScopeItem = [:]
    advantageScopeItem['name'] = "AdvantageScope"
    advantageScopeItem['version'] = advantagescopeGitTag
    config << advantageScopeItem

    def choreoItem = [:]
    choreoItem['name'] = "Choreo"
    choreoItem['version'] = choreoGitTag
    config << choreoItem

    def elasticItem = [:]
    elasticItem['name'] = "Elastic"
    elasticItem['version'] = elasticGitTag
    config << elasticItem

    def utilityItem = [:]
    utilityItem['name'] = "Utility"
    utilityItem['version'] = wpilibVersion
    config << utilityItem

    def gbuilder = getGsonBuilder()
    gbuilder.setPrettyPrinting()
    def json = gbuilder.create().toJson(config)

    toolsJson.parentFile.mkdirs()
    toolsJson.text = json
  }
}

ext.toolsConfig = {
  def toolsFolder = 'tools'
  return new Tuple2({ task->
    task.inputs.property 'toolsFolder', toolsFolder
  }, { config->
    def tools = [:]
    tools['Folder'] = toolsFolder
    tools['UpdaterJar'] = 'ToolsUpdater.jar'
    config['Tools'] = tools
  })
}

ext.toolsSetup = { AbstractArchiveTask zip->
  zip.dependsOn toolsJsonTask
  zip.inputs.file toolsJson
  zip.inputs.file scriptBaseFile
  zip.inputs.file scriptBaseUnixFile

  zip.from (toolsJson) {
    into '/tools'
  }

  if (project.ext.buildClassifier == 'Windows') {
    zip.from (scriptBaseFile) {
      into '/tools'
    }

    zip.from (scriptBaseCppFile) {
      into '/tools'
    }

    zip.from (advantageScopeScriptFile) {
      into '/tools'
    }

    zip.from (choreoScriptFile) {
      into '/tools'
    }

    zip.from (elasticScriptFile) {
      into '/tools'
    }
  } else {
    zip.from (scriptBaseUnixFile) {
      into '/tools'
      fileMode 0755
    }

    zip.from (scriptBaseCppUnixFile) {
      into '/tools'
      fileMode 0755
    }

    zip.from (advantageScopeScriptUnixFile) {
      into '/tools'
      fileMode 0755
    }

    zip.from (choreoScriptUnixFile) {
      into '/tools'
      fileMode 0755
    }

    zip.from (elasticScriptUnixFile) {
      into '/tools'
      fileMode 0755
    }
  }

}
