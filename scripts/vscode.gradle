apply from: 'scripts/versions.gradle'

def vscodeFile = file("$buildDir/vscodeConfig.json")

def vscodeWindowsZipName = "Windows.zip"
def vscodeLinuxZipName = "Linux.tar.gz"
def vscodeMacZipName = "Mac.zip"

def vscodeWindowsUrl = "https://update.code.visualstudio.com/${vsCodeVersion}/win32-x64-archive/stable".toString()

def vscodeLinuxUrl = "https://update.code.visualstudio.com/${vsCodeVersion}/linux-x64/stable".toString()

def vscodeMacUrl = "https://update.code.visualstudio.com/${vsCodeVersion}/darwin-universal/stable".toString()

def cppWindowsUrl = "https://github.com/Microsoft/vscode-cpptools/releases/download/v${cppToolsVersion}/cpptools-win64.vsix"

def cppMacUrl = "https://github.com/Microsoft/vscode-cpptools/releases/download/v${cppToolsVersion}/cpptools-osx.vsix"

def cppLinuxUrl = "https://github.com/Microsoft/vscode-cpptools/releases/download/v${cppToolsVersion}/cpptools-linux.vsix"

def wpilibExtensionUrl = "https://github.com/wpilibsuite/vscode-wpilib/releases/download/v${wpilibVersion}/vscode-wpilib-${wpilibVersion}.vsix"

def wpilibUtilWinUrl = "https://github.com/wpilibsuite/vscode-wpilib/releases/download/v${wpilibVersion}/wpilibutility-windows.zip"

def wpilibUtilLinuxUrl = "https://github.com/wpilibsuite/vscode-wpilib/releases/download/v${wpilibVersion}/wpilibutility-linux.tar.gz"

def wpilibUtilMacUrl = "https://github.com/wpilibsuite/vscode-wpilib/releases/download/v${wpilibVersion}/wpilibutility-mac.tar.gz"

def javaDebugUrl = "https://github.com/microsoft/vscode-java-debug/releases/download/${javaDebugVersion}/vscjava.vscode-java-debug-${javaDebugVersion}.vsix"

def javaLangUrl = "https://github.com/redhat-developer/vscode-java/releases/download/v${javaExtVersion}/java-${javaExtVersion}.vsix"

def javaDepUrl = "https://github.com/microsoft/vscode-java-dependency/releases/download/${javaDependencyVersion}/vscjava.vscode-java-dependency-${javaDependencyVersion}.vsix"

def wpilibExtensionDownload = tasks.register("wpilibExtensionDownload", Download) {
  src wpilibExtensionUrl
  dest "$buildDir/downloads"
  overwrite false
}

def standaloneDownload = tasks.register("standaloneDownload", Download) {

  if (project.hasProperty('linuxBuild')) {
    src wpilibUtilLinuxUrl
  } else if (project.hasProperty('macBuild') || project.hasProperty('macBuildArm')) {
    src wpilibUtilMacUrl
  } else {
    src wpilibUtilWinUrl
  }
  dest "$buildDir/downloads"
  overwrite false
}

def cppExtensionDownload = tasks.register("cppExtensionDownload", Download) {
  src cppWindowsUrl
  dest "$buildDir/downloads"
  overwrite false
}

def cppMacExtensionDownload = tasks.register("cppMacExtensionDownload", Download) {
  src cppMacUrl
  dest "$buildDir/downloads"
  overwrite false
}

def cppLinuxExtensionDownload = tasks.register("cppLinuxExtensionDownload", Download) {
  src cppLinuxUrl
  dest "$buildDir/downloads"
  overwrite false
}


def javaLangExtensionDownload = tasks.register("javaLangExtensionDownload", Download) {
  src javaLangUrl
  dest "$buildDir/downloads"
  overwrite false
}


def javaDebugExtensionDownload = tasks.register("javaDebugExtensionDownload", Download) {
  src javaDebugUrl
  dest "$buildDir/downloads"
  overwrite false
}

def javaDepExtensionDownload = tasks.register("javaDepExtensionDownload", Download) {
  src javaDepUrl
  dest "$buildDir/downloads"
  overwrite false
}

def cppDownloadTask
if (project.hasProperty('linuxBuild')) {
  cppDownloadTask = cppLinuxExtensionDownload
} else if (project.hasProperty('macBuild') || project.hasProperty('macBuildArm')) {
  cppDownloadTask = cppMacExtensionDownload
} else {
  cppDownloadTask = cppExtensionDownload
}

def getInformationOfVsix = { vsix, map->
  def pkg = vsix.matching { include '**/extension/package.json' }.files.asList()[0]
  def packageJson = new groovy.json.JsonSlurper().parseText(pkg.text)
  map['name'] = "${packageJson.publisher}.${packageJson.name}".toString()
  map['version'] = packageJson.version
}

def vscodeTask = tasks.register('vscodeConfig', Task) {
  dependsOn wpilibExtensionDownload
  dependsOn cppDownloadTask
  dependsOn javaLangExtensionDownload
  dependsOn javaDebugExtensionDownload
  dependsOn javaDepExtensionDownload

  inputs.files wpilibExtensionDownload.get().outputFiles
  inputs.files cppDownloadTask.get().outputFiles
  inputs.files javaLangExtensionDownload.get().outputFiles
  inputs.files javaDebugExtensionDownload.get().outputFiles
  inputs.files javaDepExtensionDownload.get().outputFiles

  doLast {
    def config = [:]

    def wpilibMap = [:]
    wpilibMap['vsix'] = wpilibExtensionDownload.get().outputFiles.first().name
    getInformationOfVsix(zipTree(wpilibExtensionDownload.get().outputFiles.first()), wpilibMap)
    def cppMap = [:]
    cppMap['vsix'] = cppDownloadTask.get().outputFiles.first().name
    getInformationOfVsix(zipTree(cppDownloadTask.get().outputFiles.first()), cppMap)
    def javaLangMap = [:]
    javaLangMap['vsix'] = javaLangExtensionDownload.get().outputFiles.first().name
    getInformationOfVsix(zipTree(javaLangExtensionDownload.get().outputFiles.first()), javaLangMap)
    def javaDebugMap = [:]
    javaDebugMap['vsix'] = javaDebugExtensionDownload.get().outputFiles.first().name
    getInformationOfVsix(zipTree(javaDebugExtensionDownload.get().outputFiles.first()), javaDebugMap)

    def javaDepMap = [:]
    javaDepMap['vsix'] = javaDepExtensionDownload.get().outputFiles.first().name
    getInformationOfVsix(zipTree(javaDepExtensionDownload.get().outputFiles.first()), javaDepMap)

    config['VsCodeWindowsUrl'] = vscodeWindowsUrl
    config['VsCodeWindowsName'] = vscodeWindowsZipName
    config['VsCodeMacUrl'] = vscodeMacUrl
    config['VsCodeMacName'] = vscodeMacZipName
    config['VsCodeLinuxUrl'] = vscodeLinuxUrl
    config['VsCodeLinuxName'] = vscodeLinuxZipName
    config['VsCodeVersion'] = vsCodeVersion

    config['wpilibExtension'] = wpilibMap

    config['thirdPartyExtensions'] = [
      cppMap,
      javaLangMap,
      javaDebugMap,
      javaDepMap
    ]


    def gbuilder = getGsonBuilder()

    gbuilder.setPrettyPrinting()
    def json = gbuilder.create().toJson(config)

    vscodeFile.parentFile.mkdirs()

    vscodeFile.text = json
  }
}

ext.vscodeConfigZipSetup = { AbstractArchiveTask zip->
  zip.dependsOn vscodeTask
  zip.inputs.file vscodeFile

  zip.from (vscodeFile)
}

ext.vscodeZipSetup = { AbstractArchiveTask zip->
  zip.dependsOn vscodeTask
  zip.dependsOn wpilibExtensionDownload
  zip.dependsOn standaloneDownload

  zip.inputs.file vscodeFile

  if (project.hasProperty('linuxBuild')) {
    // Linux
    zip.from(project.tarTree(project.resources.gzip(standaloneDownload.get().outputFiles.first()))) {
      into '/utility'
      includeEmptyDirs = false
    }
  } else if (project.hasProperty('macBuild') || project.hasProperty('macBuildArm')) {
    // Mac
    // Cannot extract, otherwise mac borks
    zip.from (standaloneDownload.get().outputFiles.first()) {
      into '/utility'
    }
  } else {
    // Windows
    zip.from(project.zipTree(standaloneDownload.get().outputFiles.first())) {
      into '/utility'

      includeEmptyDirs = false
    }
  }

  zip.from (vscodeFile) {
    into '/installUtils'
  }

  zip.from (javaLangExtensionDownload.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
  zip.from (javaDebugExtensionDownload.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
  zip.from (cppDownloadTask.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
  zip.from (wpilibExtensionDownload.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
  zip.from (javaDepExtensionDownload.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
}
