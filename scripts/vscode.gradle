apply from: 'scripts/versions.gradle'

def vscodeFile = file("$buildDir/vscodeConfig.json")

def vscodeWindowsZipName = "VSCode-${vsCodeVersion}-Windows.zip".toString()
def vscodeLinuxZipName = "VSCode-${vsCodeVersion}-Linux.tar.gz".toString()
def vscodeMacZipName = "VSCode-${vsCodeVersion}-Mac.zip".toString()

def vscodeWindowsUrl = "https://update.code.visualstudio.com/${vsCodeVersion}/win32-x64-archive/stable".toString()

def vscodeLinuxUrl = "https://update.code.visualstudio.com/${vsCodeVersion}/linux-x64/stable".toString()

def vscodeMacUrl = "https://update.code.visualstudio.com/${vsCodeVersion}/darwin-universal/stable".toString()

def cppWindowsUrl = "https://frcmaven.wpi.edu/api/download/vscode-extensions/ms-vscode/cpptools/ms-vscode.cpptools-${cppToolsVersion}@win32-x64.vsix"

def cppMacUrl = "https://frcmaven.wpi.edu/api/download/vscode-extensions/ms-vscode/cpptools/ms-vscode.cpptools-${cppToolsVersion}@darwin-x64.vsix"

def cppMacArmUrl = "https://frcmaven.wpi.edu/api/download/vscode-extensions/ms-vscode/cpptools/ms-vscode.cpptools-${cppToolsVersion}@darwin-arm64.vsix"

def cppLinuxUrl = "https://frcmaven.wpi.edu/api/download/vscode-extensions/ms-vscode/cpptools/ms-vscode.cpptools-${cppToolsVersion}@linux-x64.vsix"

def pythonExtUrl = "https://marketplace.visualstudio.com/_apis/public/gallery/publishers/ms-python/vsextensions/python/${pythonExtVersion}/vspackage"

def pylanceUrl = "https://marketplace.visualstudio.com/_apis/public/gallery/publishers/ms-python/vsextensions/vscode-pylance/${pylanceVersion}/vspackage"

def isortUrl = "https://marketplace.visualstudio.com/_apis/public/gallery/publishers/ms-python/vsextensions/isort/${isortVersion}/vspackage"

def autopep8Url = "https://marketplace.visualstudio.com/_apis/public/gallery/publishers/ms-python/vsextensions/autopep8/${autopep8Version}/vspackage"

def wpilibExtensionUrl = "https://github.com/wpilibsuite/vscode-wpilib/releases/download/v${wpilibVersion}/vscode-wpilib-${wpilibVersion}.vsix"

def wpilibUtilWinUrl = "https://github.com/wpilibsuite/vscode-wpilib/releases/download/v${wpilibVersion}/wpilibutility-windows.zip"

def wpilibUtilLinuxUrl = "https://github.com/wpilibsuite/vscode-wpilib/releases/download/v${wpilibVersion}/wpilibutility-linux.tar.gz"

def wpilibUtilMacUrl = "https://github.com/wpilibsuite/vscode-wpilib/releases/download/v${wpilibVersion}/wpilibutility-mac.tar.gz"

def javaDebugUrl = "https://frcmaven.wpi.edu/api/download/vscode-extensions/vscjava/vscode-java-debug/vscjava.vscode-java-debug-${javaDebugVersion}.vsix"

def javaLangUrl = "https://frcmaven.wpi.edu/api/download/vscode-extensions/redhat/java/redhat.java-${javaExtVersion}.vsix"

def javaDepUrl = "https://frcmaven.wpi.edu/api/download/vscode-extensions/vscjava/vscode-java-dependency/vscjava.vscode-java-dependency-${javaDependencyVersion}.vsix"

def vscodeWindowsDownload = tasks.register("vscodeWindowsDownload", Download) {
  src vscodeWindowsUrl
  dest "$buildDir/downloads/$vscodeWindowsZipName"
  overwrite false
}

def verifyWindows = tasks.register("verifyVscodeWindowsDownload", Verify) {
  dependsOn vscodeWindowsDownload
  inputs.files vscodeWindowsDownload.get().outputFiles.first()
  inputs.property 'hash', vscodeWindowsHash
  src vscodeWindowsDownload.get().outputFiles.first()
  algorithm 'SHA-256'
  checksum vscodeWindowsHash
}

def vscodeMacDownload = tasks.register("vscodeMacDownload", Download) {
  src vscodeMacUrl
  dest "$buildDir/downloads/$vscodeMacZipName"
  overwrite false
}

def verifyMac = tasks.register("verifyVscodeMacDownload", Verify) {
  dependsOn vscodeMacDownload
  inputs.files vscodeMacDownload.get().outputFiles.first()
  inputs.property 'hash', vscodeMacHash
  src vscodeMacDownload.get().outputFiles.first()
  algorithm 'SHA-256'
  checksum vscodeMacHash
}

def vscodeLinuxDownload = tasks.register("vscodeLinuxDownload", Download) {
  src vscodeLinuxUrl
  dest "$buildDir/downloads/$vscodeLinuxZipName"
  overwrite false
}

def verifyLinux = tasks.register("verifyVscodeLinuxDownload", Verify) {
  dependsOn vscodeLinuxDownload
  inputs.files vscodeLinuxDownload.get().outputFiles.first()
  inputs.property 'hash', vscodeLinuxHash
  src vscodeLinuxDownload.get().outputFiles.first()
  algorithm 'SHA-256'
  checksum vscodeLinuxHash
}

def wpilibExtensionDownload = tasks.register("wpilibExtensionDownload", Download) {
  src wpilibExtensionUrl
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}

def standaloneDownload = tasks.register("standaloneDownload", Download) {

  if (project.hasProperty('linuxBuild')) {
    src wpilibUtilLinuxUrl
  } else if (project.hasProperty('macBuild') || project.hasProperty('macBuildArm')) {
    src wpilibUtilMacUrl
  } else {
    src wpilibUtilWinUrl
  }
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}

def cppExtensionDownload = tasks.register("cppExtensionDownload", Download) {
  src cppWindowsUrl
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}

def cppMacExtensionDownload = tasks.register("cppMacExtensionDownload", Download) {
  src cppMacUrl
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}

def cppMacArmExtensionDownload = tasks.register("cppMacArmExtensionDownload", Download) {
  src cppMacArmUrl
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}


def cppLinuxExtensionDownload = tasks.register("cppLinuxExtensionDownload", Download) {
  src cppLinuxUrl
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}


def javaLangExtensionDownload = tasks.register("javaLangExtensionDownload", Download) {
  src javaLangUrl
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}


def javaDebugExtensionDownload = tasks.register("javaDebugExtensionDownload", Download) {
  src javaDebugUrl
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}

def javaDepExtensionDownload = tasks.register("javaDepExtensionDownload", Download) {
  src javaDepUrl
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}


def pythonExtensionDownload = tasks.register("pythonExtensionDownload", Download) {
  src pythonExtUrl
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}


def pylanceExtensionDownload = tasks.register("pylanceExtensionDownload", Download) {
  src pylanceUrl
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}


def isortExtensionDownload = tasks.register("isortExtensionDownload", Download) {
  src isortUrl
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}


def autopep8ExtensionDownload = tasks.register("autopep8ExtensionDownload", Download) {
  src autopep8Url
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}

def cppDownloadTask
if (project.hasProperty('linuxBuild')) {
  cppDownloadTask = cppLinuxExtensionDownload
} else if (project.hasProperty('macBuild')) {
  cppDownloadTask = cppMacExtensionDownload
} else if (project.hasProperty('macBuildArm')) {
  cppDownloadTask = cppMacArmExtensionDownload
} else {
  cppDownloadTask = cppExtensionDownload
}

def getInformationOfVsix = { vsix, map->
  def pkg = vsix.matching { include '**/extension/package.json' }.files.asList()[0]
  def packageJson = new groovy.json.JsonSlurper().parseText(pkg.text)
  map['name'] = "${packageJson.publisher}.${packageJson.name}".toString()
  map['version'] = packageJson.version
}

def vscodeTask = tasks.register('vscodeConfig', Task) {
  dependsOn wpilibExtensionDownload
  dependsOn cppDownloadTask
  dependsOn javaLangExtensionDownload
  dependsOn javaDebugExtensionDownload
  dependsOn javaDepExtensionDownload
  dependsOn pythonExtensionDownload
  dependsOn pylanceExtensionDownload
  dependsOn isortExtensionDownload
  dependsOn autopep8ExtensionDownload
  dependsOn verifyVscodeLinuxDownload
  dependsOn verifyVscodeWindowsDownload
  dependsOn verifyVscodeMacDownload

  inputs.files wpilibExtensionDownload.get().outputFiles
  inputs.files cppDownloadTask.get().outputFiles
  inputs.files javaLangExtensionDownload.get().outputFiles
  inputs.files javaDebugExtensionDownload.get().outputFiles
  inputs.files javaDepExtensionDownload.get().outputFiles
  inputs.files pythonExtensionDownload.get().outputFiles
  inputs.files pylanceExtensionDownload.get().outputFiles
  inputs.files isortExtensionDownload.get().outputFiles
  inputs.files autopep8ExtensionDownload.get().outputFiles

  inputs.property 'linuxhash', vscodeLinuxHash
  inputs.property 'machash', vscodeMacHash
  inputs.property 'winhash', vscodeWindowsHash

  doLast {
    def config = [:]

    def wpilibMap = [:]
    wpilibMap['vsix'] = wpilibExtensionDownload.get().outputFiles.first().name
    getInformationOfVsix(zipTree(wpilibExtensionDownload.get().outputFiles.first()), wpilibMap)
    def cppMap = [:]
    cppMap['vsix'] = cppDownloadTask.get().outputFiles.first().name
    getInformationOfVsix(zipTree(cppDownloadTask.get().outputFiles.first()), cppMap)
    def javaLangMap = [:]
    javaLangMap['vsix'] = javaLangExtensionDownload.get().outputFiles.first().name
    getInformationOfVsix(zipTree(javaLangExtensionDownload.get().outputFiles.first()), javaLangMap)
    def javaDebugMap = [:]
    javaDebugMap['vsix'] = javaDebugExtensionDownload.get().outputFiles.first().name
    getInformationOfVsix(zipTree(javaDebugExtensionDownload.get().outputFiles.first()), javaDebugMap)
    def javaDepMap = [:]
    javaDepMap['vsix'] = javaDepExtensionDownload.get().outputFiles.first().name
    getInformationOfVsix(zipTree(javaDepExtensionDownload.get().outputFiles.first()), javaDepMap)
    def pythonMap = [:]
    pythonMap['vsix'] = pythonExtensionDownload.get().outputFiles.first().name
    getInformationOfVsix(zipTree(pythonExtensionDownload.get().outputFiles.first()), pythonMap)
    def pylanceMap = [:]
    pylanceMap['vsix'] = pylanceExtensionDownload.get().outputFiles.first().name
    getInformationOfVsix(zipTree(pylanceExtensionDownload.get().outputFiles.first()), pylanceMap)
    def isortMap = [:]
    isortMap['vsix'] = isortExtensionDownload.get().outputFiles.first().name
    getInformationOfVsix(zipTree(isortExtensionDownload.get().outputFiles.first()), isortMap)
    def autopep8Map = [:]
    autopep8Map['vsix'] = autopep8ExtensionDownload.get().outputFiles.first().name
    getInformationOfVsix(zipTree(autopep8ExtensionDownload.get().outputFiles.first()), autopep8Map)

    config['VsCodeWindowsUrl'] = vscodeWindowsUrl
    config['VsCodeWindowsName'] = vscodeWindowsZipName
    config['VsCodeWindowsHash'] = vscodeWindowsHash
    config['VsCodeMacUrl'] = vscodeMacUrl
    config['VsCodeMacName'] = vscodeMacZipName
    config['VsCodeMacHash'] = vscodeMacHash
    config['VsCodeLinuxUrl'] = vscodeLinuxUrl
    config['VsCodeLinuxName'] = vscodeLinuxZipName
    config['VsCodeLinuxHash'] = vscodeLinuxHash
    config['VsCodeVersion'] = vsCodeVersion

    config['wpilibExtension'] = wpilibMap

    config['thirdPartyExtensions'] = [
      cppMap,
      javaLangMap,
      javaDebugMap,
      javaDepMap,
      pythonMap,
      pylanceMap,
      isortMap,
      autopep8Map
    ]


    def gbuilder = getGsonBuilder()

    gbuilder.setPrettyPrinting()
    def json = gbuilder.create().toJson(config)

    vscodeFile.parentFile.mkdirs()

    vscodeFile.text = json
  }
}

ext.vscodeConfigZipSetup = { AbstractArchiveTask zip->
  zip.dependsOn vscodeTask
  zip.inputs.file vscodeFile

  zip.from (vscodeFile)
}

ext.vscodeZipSetup = { AbstractArchiveTask zip->
  zip.dependsOn vscodeTask
  zip.dependsOn wpilibExtensionDownload
  zip.dependsOn standaloneDownload

  zip.inputs.file vscodeFile

  if (project.hasProperty('linuxBuild')) {
    // Linux
    zip.from(project.tarTree(project.resources.gzip(standaloneDownload.get().outputFiles.first()))) {
      into '/utility'
      includeEmptyDirs = false
    }
  } else if (project.hasProperty('macBuild') || project.hasProperty('macBuildArm')) {
    // Mac
    // Cannot extract, otherwise mac borks
    zip.from (standaloneDownload.get().outputFiles.first()) {
      into '/utility'
    }
  } else {
    // Windows
    zip.from(project.zipTree(standaloneDownload.get().outputFiles.first())) {
      into '/utility'

      includeEmptyDirs = false
    }
  }

  zip.from (vscodeFile) {
    into '/installUtils'
  }

  zip.from (javaLangExtensionDownload.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
  zip.from (javaDebugExtensionDownload.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
  zip.from (cppDownloadTask.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
  zip.from (wpilibExtensionDownload.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
  zip.from (javaDepExtensionDownload.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
  zip.from (pythonExtensionDownload.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
  zip.from (pylanceExtensionDownload.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
  zip.from (isortExtensionDownload.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
  zip.from (autopep8ExtensionDownload.get().outputFiles.first()) {
    into '/vsCodeExtensions'
  }
}
