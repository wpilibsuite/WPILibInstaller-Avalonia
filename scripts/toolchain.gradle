def gitTag = 'v2022-2'
def baseUrl = "https://github.com/wpilibsuite/roborio-toolchain/releases/download/$gitTag/"

def gccVersion = '7.3.0'

def fileName32 = 'FRC-2022-Windows32-Toolchain-7.3.0.zip'
def fileName64 = 'FRC-2022-Windows64-Toolchain-7.3.0.zip'

def downloadUrl32 = baseUrl + fileName32
def fileWin32 = file("$buildDir/$fileName32")

def downloadUrl64 = baseUrl + fileName64
def fileWin64 = file("$buildDir/$fileName64")

def fileNameMac = 'FRC-2022-Mac-Toolchain-7.3.0.tar.gz'

def downloadUrlMac = baseUrl + fileNameMac
def fileMac = file("$buildDir/$fileNameMac")

def fileNameLinux = 'FRC-2022-Linux-Toolchain-7.3.0.tar.gz'

def downloadUrlLinux = baseUrl + fileNameLinux
def fileLinux = file("$buildDir/$fileNameLinux")

apply plugin: 'de.undercouch.download'

def downloadTask32 = tasks.register('downloadToolchain32', Download) {
  src downloadUrl32
  dest buildDir
  overwrite false
}

def downloadTask64 = tasks.register('downloadToolchain64', Download) {
  src downloadUrl64
  dest buildDir
  overwrite false
}



def downloadTaskMac = tasks.register('downloadToolchainMac', Download) {
  src downloadUrlMac
  dest buildDir
  overwrite false
}

def downloadTaskLinux = tasks.register('downloadToolchainLinux', Download) {
  src downloadUrlLinux
  dest buildDir
  overwrite false
}

ext.toolchainConfigTaskSetup = {
  return new Tuple2({ task->
    task.inputs.property 'toolchainName', fileName
  }, { config->
    def toolchainConfig = [:];

    toolchainConfig['Version'] = gccVersion
    toolchainConfig['Directory'] = 'roborio'

    config['CppToolchain'] = toolchainConfig
  });
}

ext.toolchainZipSetup = { AbstractArchiveTask zip->
  if (project.hasProperty('linuxBuild')) {
    zip.dependsOn downloadTaskLinux

    zip.inputs.file fileLinux

    zip.from(project.tarTree(project.resources.gzip(fileLinux))) {

      eachFile { f->
        f.path = f.path.replace('frc2022/roborio/', 'roborio/')
      }

      includeEmptyDirs = false
    }

  } else if (project.hasProperty('macBuild')) {
    zip.dependsOn downloadTaskMac

    zip.inputs.file fileMac

    zip.from(project.tarTree(project.resources.gzip(fileMac))) {

      eachFile { f->
        f.path = f.path.replace('frc2022/roborio/', 'roborio/')
      }

      includeEmptyDirs = false
    }
  } else if (project.hasProperty('windows32')) {
    zip.dependsOn downloadTask32

    zip.inputs.file fileWin32

    zip.from(project.zipTree(fileWin32)) {

      eachFile { f->
        f.path = f.path.replace('frc2022/roborio/', 'roborio/')
      }

      includeEmptyDirs = false
    }
  } else {
    zip.dependsOn downloadTask64

    zip.inputs.file fileWin64

    zip.from(project.zipTree(fileWin64)) {

      eachFile { f->
        f.path = f.path.replace('frc2022/roborio/', 'roborio/')
      }

      includeEmptyDirs = false
    }
  }
}
