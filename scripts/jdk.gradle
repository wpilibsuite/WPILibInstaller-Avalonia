
evaluationDependsOn(':gradleriobase')

apply plugin: 'de.undercouch.download'


def downloadLinuxJdk = tasks.register('downloadLinuxJdk', Download) {
  src 'https://github.com/adoptium/temurin11-binaries/releases/download/jdk-11.0.12%2B7/OpenJDK11U-jdk_x64_linux_hotspot_11.0.12_7.tar.gz'
  dest buildDir
  overwrite false
}

def jdkLinuxFile = file("$buildDir/OpenJDK11U-jdk_x64_linux_hotspot_11.0.12_7.tar.gz")

def downloadMacJdk = tasks.register('downloadMacJdk', Download) {
  src 'https://github.com/adoptium/temurin11-binaries/releases/download/jdk-11.0.12%2B7/OpenJDK11U-jdk_x64_mac_hotspot_11.0.12_7.tar.gz'
  dest buildDir
  overwrite false
}

def jdkMacFile = file("$buildDir/OpenJDK11U-jdk_x64_mac_hotspot_11.0.12_7.tar.gz")

def download32BitJdk = tasks.register('download32BitJdk', Download) {
  src 'https://github.com/adoptium/temurin11-binaries/releases/download/jdk-11.0.12%2B7/OpenJDK11U-jdk_x86-32_windows_hotspot_11.0.12_7.zip'
  dest buildDir
  overwrite false
}

def jdk32File = file("$buildDir/OpenJDK11U-jdk_x86-32_windows_hotspot_11.0.12_7.zip")

def download64BitJdk = tasks.register('download64BitJdk', Download) {
  src 'https://github.com/adoptium/temurin11-binaries/releases/download/jdk-11.0.12%2B7/OpenJDK11U-jdk_x64_windows_hotspot_11.0.12_7.zip'
  dest buildDir
  overwrite false
}

def jdk64File = file("$buildDir/OpenJDK11U-jdk_x64_windows_hotspot_11.0.12_7.zip")

configurations {
  runtimex86
  runtimex64
}

dependencies {
    if (project.ext.buildClassifier == 'Windows32') {
        runtimex86 "edu.wpi.first.msvc:runtime:${project(':gradleriobase').wpi.versions.wpilibVersion}:x86@zip"
    } else if (project.ext.buildClassifier == 'Windows64') {
        runtimex64 "edu.wpi.first.msvc:runtime:${project(':gradleriobase').wpi.versions.wpilibVersion}:x64@zip"
    }
}

def runtimeExtractTask = tasks.register('copyAndExtractRuntime', Copy) {
  into 'build/runtime'
  if (project.ext.buildClassifier == 'Windows32') {
    from(project.zipTree(configurations.runtimex86.resolvedConfiguration.resolvedArtifacts.first().file))
  } else if (project.ext.buildClassifier == 'Windows64') {
    from(project.zipTree(configurations.runtimex64.resolvedConfiguration.resolvedArtifacts.first().file))
  }
}

def jdkConfigFile = file("$buildDir/jdkconfig.json")

def jdkConfigFileTask = tasks.register("jdkConfigFile") {
  it.outputs.file jdkConfigFile

  doLast {

    def config = [:]
    config['folder'] = 'jdk'
    config['tarFile'] = 'jdk.tar.gz'

    def gbuilder = getGsonBuilder()

    gbuilder.setPrettyPrinting()
    def json = gbuilder.create().toJson(config)

    jdkConfigFile.parentFile.mkdirs()

    jdkConfigFile.text = json
  }
}

ext.jdkConfigFileSetup = { AbstractArchiveTask zip->
  zip.dependsOn jdkConfigFileTask
  zip.inputs.file jdkConfigFile

  zip.from(jdkConfigFile) {
    rename {'jdkConfig.json'}
  }
}

ext.jdkZipSetup32 = { AbstractArchiveTask zip->
  zip.dependsOn download32BitJdk
  zip.dependsOn jdkConfigFileTask
  zip.dependsOn runtimeExtractTask

  zip.inputs.file jdk32File
  zip.inputs.file jdkConfigFile

  zip.from(project.zipTree(jdk32File)) {

    eachFile { f->
      f.path = f.path.replace('jdk-11.0.12+7/', 'jdk/')
    }

    eachFile { f->
      file('build/runtime').eachFile {
        if (f.name.endsWith(it.name)) {
          f.exclude()
        }
      }
    }

    exclude '**/src.zip'
    exclude '**/bin/*.pdb'
    exclude '**/bin/*.map'
    exclude '**/bin/server/*.pdb'
    exclude '**/bin/server/*.map'
    exclude '**/demo/**'

    includeEmptyDirs = false
  }

  zip.from (runtimeExtractTask.get()) {
    into '/jdk/bin'
  }

  zip.from(jdkConfigFile) {
    into '/installUtils'
    rename {'jdkConfig.json'}
  }
}

ext.jdkZipSetup64 = { AbstractArchiveTask zip->
  zip.dependsOn download64BitJdk
  zip.dependsOn jdkConfigFileTask
  zip.dependsOn runtimeExtractTask

  zip.inputs.file jdk64File
  zip.inputs.file jdkConfigFile

  zip.from(project.zipTree(jdk64File)) {

    eachFile { f->
      f.path = f.path.replace('jdk-11.0.12+7/', 'jdk/')
    }

    eachFile { f->
      file('build/runtime').eachFile {
        if (f.name.endsWith(it.name)) {
          f.exclude()
        }
      }
    }

    exclude '**/src.zip'
    exclude '**/bin/*.pdb'
    exclude '**/bin/*.map'
    exclude '**/bin/server/*.pdb'
    exclude '**/bin/server/*.map'
    exclude '**/demo/**'

    includeEmptyDirs = false
  }

  zip.from (runtimeExtractTask.get()) {
    into '/jdk/bin'
  }

  zip.from(jdkConfigFile) {
    into '/installUtils'
    rename {'jdkConfig.json'}
  }
}

ext.jdkZipSetupLinux = { AbstractArchiveTask zip->
  zip.dependsOn downloadLinuxJdk
  zip.dependsOn jdkConfigFileTask

  zip.inputs.file jdkLinuxFile
  zip.inputs.file jdkConfigFile

  zip.from(project.tarTree(project.resources.gzip(jdkLinuxFile))) {
    eachFile { f->
      f.path = f.path.replace('jdk-11.0.12+7/', 'jdk/')
    }

    exclude '**/src.zip'
    exclude '**/bin/*.pdb'
    exclude '**/bin/*.map'
    exclude '**/bin/server/*.pdb'
    exclude '**/bin/server/*.map'
    exclude '**/demo/**'

    includeEmptyDirs = false
  }

  zip.from(jdkConfigFile) {
    into '/installUtils'
    rename {'jdkConfig.json'}
  }
}

ext.jdkZipSetupMac = { AbstractArchiveTask zip->
  zip.dependsOn downloadMacJdk
  zip.dependsOn jdkConfigFileTask

  zip.inputs.file jdkMacFile
  zip.inputs.file jdkConfigFile

  zip.from(project.tarTree(project.resources.gzip(jdkMacFile))) {
    eachFile { f ->
      f.path = f.path.replace('jdk-11.0.12+7/Contents/Home/', 'jdk/')
    }

    exclude './jdk-11.0.12+7/Contents/MacOS/**'
    exclude './jdk-11.0.12+7/Contents/Info.plist'

    exclude '**/src.zip'
    exclude '**/bin/*.pdb'
    exclude '**/bin/*.map'
    exclude '**/bin/server/*.pdb'
    exclude '**/bin/server/*.map'
    exclude '**/demo/**'

    includeEmptyDirs = false
  }

    zip.from(jdkConfigFile) {
    into '/installUtils'
    rename {'jdkConfig.json'}
  }
}

ext.jdkZipSetup = { AbstractArchiveTask zip ->
  if (project.hasProperty('linuxBuild')) {
    jdkZipSetupLinux(zip)
  } else if (project.hasProperty('macBuild')) {
    jdkZipSetupMac(zip)
  } else if (project.hasProperty('windows32')) {
    jdkZipSetup32(zip)
  } else {
    jdkZipSetup64(zip)
  }
}
