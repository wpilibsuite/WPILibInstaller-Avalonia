
evaluationDependsOn(':gradleriobase')

apply plugin: 'de.undercouch.download'
apply from: 'scripts/versions.gradle'

def jdkVersionUnderscore = jdkVersion.replace('+', '_')
def jdkVersionEscaped = jdkVersion.replace('+', '%2B')

def downloadLinuxJdk = tasks.register('downloadLinuxJdk', Download) {
  src "https://github.com/adoptium/temurin17-binaries/releases/download/jdk-$jdkVersionEscaped/OpenJDK17U-jdk_x64_linux_hotspot_${jdkVersionUnderscore}.tar.gz"
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}

def downloadLinuxArm64Jdk = tasks.register('downloadLinuxJdkArm64', Download) {
  src "https://github.com/adoptium/temurin17-binaries/releases/download/jdk-$jdkVersionEscaped/OpenJDK17U-jdk_aarch64_linux_hotspot_${jdkVersionUnderscore}.tar.gz"
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}

def downloadMacJdk = tasks.register('downloadMacJdk', Download) {
  src "https://github.com/adoptium/temurin17-binaries/releases/download/jdk-$jdkVersionEscaped/OpenJDK17U-jdk_x64_mac_hotspot_${jdkVersionUnderscore}.tar.gz"
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}

def downloadMacArmJdk = tasks.register('downloadMacArmJdk', Download) {
  src "https://github.com/adoptium/temurin17-binaries/releases/download/jdk-$jdkVersionEscaped/OpenJDK17U-jdk_aarch64_mac_hotspot_${jdkVersionUnderscore}.tar.gz"
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}

def downloadWindowsJdk = tasks.register('downloadWindowsJdk', Download) {
  src "https://github.com/adoptium/temurin17-binaries/releases/download/jdk-$jdkVersionEscaped/OpenJDK17U-jdk_x64_windows_hotspot_${jdkVersionUnderscore}.zip"
  def fileName = file(src.file).name
  dest "$buildDir/downloads/$fileName"
  overwrite false
}

configurations {
  runtimex64
}

dependencies {
    if (project.ext.buildClassifier == 'Windows') {
        runtimex64 "edu.wpi.first.msvc:runtime:${project(':gradleriobase').wpi.versions.wpilibVersion}:x64@zip"
    }
}

def runtimeExtractTask = tasks.register('copyAndExtractRuntime', Copy) {
  into 'build/runtime'
  if (project.ext.buildClassifier == 'Windows') {
    from(project.zipTree(configurations.runtimex64.resolvedConfiguration.resolvedArtifacts.first().file))
  }
}

def jdkConfigFile = file("$buildDir/jdkconfig.json")

def jdkConfigFileTask = tasks.register("jdkConfigFile") {
  it.outputs.file jdkConfigFile

  doLast {

    def config = [:]
    config['folder'] = 'jdk'
    config['tarFile'] = 'jdk.tar.gz'

    def gbuilder = getGsonBuilder()

    gbuilder.setPrettyPrinting()
    def json = gbuilder.create().toJson(config)

    jdkConfigFile.parentFile.mkdirs()

    jdkConfigFile.text = json
  }
}

ext.jdkConfigFileSetup = { AbstractArchiveTask zip->
  zip.dependsOn jdkConfigFileTask
  zip.inputs.file jdkConfigFile

  zip.from(jdkConfigFile) {
    rename {'jdkConfig.json'}
  }
}

ext.jdkZipSetupWindows = { AbstractArchiveTask zip->
  zip.dependsOn downloadWindowsJdk
  zip.dependsOn jdkConfigFileTask
  zip.dependsOn runtimeExtractTask

  zip.inputs.files downloadWindowsJdk.get().outputFiles
  zip.inputs.file jdkConfigFile

  zip.from(project.zipTree(downloadWindowsJdk.get().outputFiles.first())) {

    eachFile { f->
      f.path = f.path.replace("jdk-${jdkVersion}/", 'jdk/')
    }

    eachFile { f->
      file('build/runtime').eachFile {
        if (f.name.endsWith(it.name)) {
          f.exclude()
        }
      }
    }

    exclude '**/bin/*.pdb'
    exclude '**/bin/*.map'
    exclude '**/bin/server/*.pdb'
    exclude '**/bin/server/*.map'
    exclude '**/demo/**'

    includeEmptyDirs = false
  }

  zip.from (runtimeExtractTask.get()) {
    into '/jdk/bin'
  }

  zip.from(jdkConfigFile) {
    into '/installUtils'
    rename {'jdkConfig.json'}
  }
}

ext.jdkZipSetupLinux = { AbstractArchiveTask zip->
  zip.dependsOn downloadLinuxJdk
  zip.dependsOn jdkConfigFileTask

  zip.inputs.files downloadLinuxJdk.get().outputFiles
  zip.inputs.file jdkConfigFile

  zip.from(project.tarTree(project.resources.gzip(downloadLinuxJdk.get().outputFiles.first()))) {
    eachFile { f->
      f.path = f.path.replace("jdk-${jdkVersion}/", 'jdk/')
    }

    exclude '**/bin/*.pdb'
    exclude '**/bin/*.map'
    exclude '**/bin/server/*.pdb'
    exclude '**/bin/server/*.map'
    exclude '**/demo/**'

    includeEmptyDirs = false
  }

  zip.from(jdkConfigFile) {
    into '/installUtils'
    rename {'jdkConfig.json'}
  }
}

ext.jdkZipSetupLinuxArm64 = { AbstractArchiveTask zip->
  zip.dependsOn downloadLinuxArm64Jdk
  zip.dependsOn jdkConfigFileTask

  zip.inputs.files downloadLinuxArm64Jdk.get().outputFiles
  zip.inputs.file jdkConfigFile

  zip.from(project.tarTree(project.resources.gzip(downloadLinuxArm64Jdk.get().outputFiles.first()))) {
    eachFile { f->
      f.path = f.path.replace("jdk-${jdkVersion}/", 'jdk/')
    }

    exclude '**/bin/*.pdb'
    exclude '**/bin/*.map'
    exclude '**/bin/server/*.pdb'
    exclude '**/bin/server/*.map'
    exclude '**/demo/**'

    includeEmptyDirs = false
  }

  zip.from(jdkConfigFile) {
    into '/installUtils'
    rename {'jdkConfig.json'}
  }
}

ext.jdkZipSetupMac = { AbstractArchiveTask zip->
  zip.dependsOn downloadMacJdk
  zip.dependsOn jdkConfigFileTask

  zip.inputs.files downloadMacJdk.get().outputFiles
  zip.inputs.file jdkConfigFile

  zip.from(project.tarTree(project.resources.gzip(downloadMacJdk.get().outputFiles.first()))) {
    eachFile { f ->
      f.path = f.path.replace("jdk-${jdkVersion}/Contents/Home/", 'jdk/')
    }

    exclude "./jdk-${jdkVersion}/Contents/MacOS/**"
    exclude "./jdk-${jdkVersion}/Contents/Info.plist"

    exclude '**/bin/*.pdb'
    exclude '**/bin/*.map'
    exclude '**/bin/server/*.pdb'
    exclude '**/bin/server/*.map'
    exclude '**/demo/**'

    includeEmptyDirs = false
  }

    zip.from(jdkConfigFile) {
    into '/installUtils'
    rename {'jdkConfig.json'}
  }
}

ext.jdkZipSetupMacArm = { AbstractArchiveTask zip->
  zip.dependsOn downloadMacArmJdk
  zip.dependsOn jdkConfigFileTask

  zip.inputs.files downloadMacArmJdk.get().outputFiles
  zip.inputs.file jdkConfigFile

  zip.from(project.tarTree(project.resources.gzip(downloadMacArmJdk.get().outputFiles.first()))) {
    eachFile { f ->
      f.path = f.path.replace("jdk-${jdkVersion}/Contents/Home/", 'jdk/')
    }

    exclude "./jdk-${jdkVersion}/Contents/MacOS/**"
    exclude "./jdk-${jdkVersion}/Contents/Info.plist"

    exclude '**/bin/*.pdb'
    exclude '**/bin/*.map'
    exclude '**/bin/server/*.pdb'
    exclude '**/bin/server/*.map'
    exclude '**/demo/**'

    includeEmptyDirs = false
  }

    zip.from(jdkConfigFile) {
    into '/installUtils'
    rename {'jdkConfig.json'}
  }
}

ext.jdkZipSetup = { AbstractArchiveTask zip ->
  if (project.hasProperty('linuxBuild')) {
    jdkZipSetupLinux(zip)
  } else if (project.hasProperty('linuxBuildArm64')) {
    jdkZipSetupLinuxArm64(zip)
  } else if (project.hasProperty('macBuild')) {
    jdkZipSetupMac(zip)
  } else if (project.hasProperty('macBuildArm')) {
    jdkZipSetupMacArm(zip)
  } else {
    jdkZipSetupWindows(zip)
  }
}
